#!/bin/sh
# the next line restarts using tclsh \
exec tclsh "$0" "$@"

# Rivet test suite, by David N. Welton <davidw@apache.org>

# See README file for more information.

# $Id$ 

package require tcltest
package require http 2.1

set urlbase "http://localhost:8080/"
set testfilename1 "rivet1-test.ttml"
set testfilename2 "rivet2-test.tcl"

# Test stanzas are created by giving the test a name and a
# description.  The code is then executed, and the results compared
# with the desired result, which is placed after the block of code.
# See man tcltest for more information.

::tcltest::test hello-1.1 {hello world test} {
    set page [ ::http::geturl "${urlbase}$testfilename1" ]
    regexp -line {^Hello, World$} [ ::http::data $page ] match
    set match
} {Hello, World}

::tcltest::test i18n-1.1 {I18N test} {
    set page [ ::http::geturl "${urlbase}$testfilename1" ]
    regexp -line {^¡ À È Ì Ò Ù - El Burro Sabe Más Que Tú!$} [ ::http::data $page ] match
    set match
} {¡ À È Ì Ò Ù - El Burro Sabe Más Que Tú!}

::tcltest::test getvariables-1.1 {GET variables} {
    set page [ ::http::geturl "${urlbase}$testfilename1?foobar=goober" ]
    regexp -line {\[var get foobar\] = goober$} [ ::http::data $page ] match
    set match
} {[var get foobar] = goober}

::tcltest::test getvariables-1.2 {GET variables + I18N} {
    set page [ ::http::geturl "${urlbase}$testfilename1?Más=Tú" ]
    regexp -line {^\[var get Más\] = Tú$} [ ::http::data $page ] match
    set match
} {[var get Más] = Tú}

::tcltest::test getvariables-1.3 {GET variables + I18N + encoding} {
    set page [ ::http::geturl [ format "${urlbase}$testfilename1?%s" [ ::http::formatQuery Más Tú ] ] ]
    regexp -line {^\[var get Más\] = Tú$} [ ::http::data $page ] match
    set match
} {[var get Más] = Tú}

::tcltest::test postvariables-1.1 {POST variables} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -query foobar=goober ]
    regexp -line {^\[var get foobar\] = goober$} [ ::http::data $page ] match
    set match
} {[var get foobar] = goober}

::tcltest::test postvariables-1.2 {POST variables + I18N} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -query Más=Tú ]
    regexp -line {^\[var get Más\] = Tú$} [ ::http::data $page ] match
    set match
} {[var get Más] = Tú}

::tcltest::test postvariables-1.3 {POST variables + I18N + encoding} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -query [ ::http::formatQuery Más Tú ] ]
    regexp -line {^\[var get Más\] = Tú$} [ ::http::data $page ] match
    set match
} {[var get Más] = Tú}

::tcltest::test multivariables-1.1 {multiple variables: foo=1&foo=2} {
    set page [ ::http::geturl "${urlbase}$testfilename1?foobar=1&foobar=2&foobar=foo+bar" ]
    regexp -line {^\[var get foobar\] = 1 2 foo bar$} [ ::http::data $page ] match
    set match
} {[var get foobar] = 1 2 foo bar}

::tcltest::test env-1.1 {Environment variable} {
    set page [ ::http::geturl "${urlbase}$testfilename1" ]
    regexp -line "^env\\(DOCUMENT_NAME\\) = $testfilename1\$" [ ::http::data $page ] match
    set match
} "env(DOCUMENT_NAME) = $testfilename1"

::tcltest::test cookies-1.1 {Cookies} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -headers {Cookie "foo=bar"} ]
    regexp -line {^cookies\(foo\) = bar$} [ ::http::data $page ] match
    set match
} {cookies(foo) = bar}

::tcltest::test cookies-1.2 {Cookies + I18N} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -headers {Cookie "Más=Tú"} ]
    regexp -line {^cookies\(Más\) = Tú$} [ ::http::data $page ] match
    set match
} {cookies(Más) = Tú}

::tcltest::test cookies-1.3 {Cookies + I18N + encoding} {
    set page [ ::http::geturl "${urlbase}$testfilename1" -headers [ list Cookie [ ::http::formatQuery Más Tú ] ] ]
    regexp -line {^cookies\(Más\) = Tú$} [ ::http::data $page ] match
    set match
} {cookies(Más) = Tú}

::tcltest::test cookies-1.4 {Multiple Cookies} {
    set rslt 0
    set page [ ::http::geturl "${urlbase}$testfilename1" -headers {Cookie "bop; foo=bar;doo=wah; shoo=be ;doooo=bee;dot=dow  "} ]
    set pgdata [ ::http::data $page ]
    incr rslt [ regexp -line {^cookies\(foo\) = bar$} $pgdata ]
    incr rslt [ regexp -line {^cookies\(doo\) = wah} $pgdata ]
    incr rslt [ regexp -line {^cookies\(shoo\) = be} $pgdata ]
    incr rslt [ regexp -line {^cookies\(doooo\) = bee} $pgdata ]
    incr rslt [ regexp -line {^cookies\(dot\) = dow$} $pgdata ]
    incr rslt [ regexp -line {^cookies\(bop\) = } $pgdata ]
} 6

::tcltest::test servercookies-1.1 {Cookies from Server} {
    set rslt 0
    set page [ ::http::geturl "${urlbase}$testfilename1" ]
    upvar 0 $page state
    array set statehash $state(meta)
    regexp -line {mod=rivet; path=[^;]*; expires=01-01-2003} $statehash(Set-Cookie)
} 1

::tcltest::test tclfile-1.1 {Plain .tcl file} {
    set page [ ::http::geturl "${urlbase}$testfilename2" ]
    set pgdata [string trim [ ::http::data $page ] ]
} "¡ À È Ì Ò Ù - El Burro Sabe Más Que Tú!"

::tcltest::cleanupTests
